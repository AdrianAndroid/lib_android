/*
 * Copyright (C) 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id("AndroidXPlugin")
    id("com.android.test")
    id("kotlin-android")
}

android {
    targetProjectPath = ":benchmark:integration-tests:macrobenchmark-target"
    experimentalProperties["android.experimental.self-instrumenting"] = true

    // note: below is optional, mimicing eventual benchmark module setup
    buildTypes {
        release {
            debuggable = true
        }
    }

    defaultConfig {
        minSdkVersion 28
    }
}

// note: below is optional, mimicing eventual benchmark module setup
androidComponents {
    beforeVariants(selector().all()) {
        // Enable only the release buildType, since we only want to measure
        // release build performance
        getLogger().info("setting enable for variant" + name + ", buildType " + buildType)
        enabled = buildType == 'release'
    }
}

dependencies {
    implementation(libs.kotlinStdlib)
    implementation(libs.testRules)
    implementation(libs.testExtJunit)
    implementation(libs.testCore)
    implementation(libs.testRunner)
}
